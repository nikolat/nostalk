//******************************************************************************
// required event
//******************************************************************************
OnFirstBoot          { '\1\s[10]\0\s[0]\e' }
OnShellChanged       { '\1\s[10]\0\s[0]\e' }
OnWindowStateRestore { '\1\s[10]\0\s[0]\e' }
OnGhostChanged       { OnBoot() }
OnBoot               { '\1\s[10]\0\s[0]\e' }
OnClose              { '\1\s[10]\0\s[0]\_w[500]\-' }

//******************************************************************************
// update event
//******************************************************************************
OnUpdateBegin        { '\0\s[0]\_qUpdate Begin.\e' }
OnUpdateComplete     { '\0\s[0]\_q' + SHIORI3FW.EscapeAllTags(reference[0]) + '.\e' }
OnUpdateFailure      { '\0\s[0]\_q' + SHIORI3FW.EscapeAllTags(reference[0]) + '.\e' }
OnUpdateReady
{
	_n = 0
	if sender == 'embryo' {
		_n = TOINT(reference[0]) + 1
	} else {
		_n = TOINT(reference[0])
	}
	if _n > 1 {
		'\0\s[0]\_q' + _n + ' files exist.\_q\e'
	} elseif _n == 1 {
		'\0\s[0]\_q' + _n + ' file exists.\_q\e'
	}
}

//******************************************************************************
// mouse event
//******************************************************************************
OnMouseDoubleClick
{
	AYATEMPLATE.MouseEventExec('MouseDoubleClick');
}
MouseDoubleClick0
{
	sys.fnc.OpenMenu()
}
sys.fnc.OpenMenu
{
	sys.save.disable_sstp = 1
	"\0\s[0]\![bind,透明度,半透明,0]\_q/
	\![*]\__q[OpenAngolmois]Open Angolmois\__q\n/
	\![*]\__q[OpenNokauka]Open nokauka\__q\n\n/
	\![*]\__q[OpenUnyuReaction]Open Unyu's Reaction Page\__q\n/
	\![*]\__q[OpenZaplinejp]Open zapline-jp\__q\n\n/
	\![*]\__q[Close]Close\__q/
	\_l[0,0]\f[align,right]/
	\__q[help]\_b[%(GETSETTING('coreinfo.path') + 'image\help.png'),inline,--clipping=0 0 16 16]\__q/
	\e"
}
//******************************************************************************
// select event
//******************************************************************************
OnChoiceSelect
{
	sys.save.disable_sstp = ''
	_id = reference[0]
	if ISFUNC('sys.cs.' + _id) {
		EVAL('sys.cs.' + _id)
	}
}
sys.cs.OpenAngolmois
{
	'\![open,browser,https://nikolat.github.io/angolmois/]\e'
}
sys.cs.OpenNokauka
{
	_plugin_id = 'ef757920-2bd0-11ef-8465-0800200c9a66'
	if ASEARCH(_plugin_id, installedplugin_id) >= 0 {
		'\![raiseplugin,ef757920-2bd0-11ef-8465-0800200c9a66,OnMenuExec]\e'
	}
	else {
		'\![open,browser,https://github.com/nikolat/nokauka]\e'
	}
}
sys.cs.OpenUnyuReaction
{
	'\![open,browser,https://nostter.app/unyu@nikolat.github.io/reactions]\e'
}
sys.cs.OpenZaplinejp
{
	'\![open,browser,https://tiltpapa.github.io/zapline-jp/]\e'
}
sys.cs.Close
{
	Nostr.Hide()
}
sys.cs.help
{
	sys.fnc.ShowHelp()
}
sys.fnc.ShowHelp
{
	sys.save.disable_sstp = 1
	'\0\s[0]\![bind,透明度,半透明,0]\_qshortcut keys:\n\n/
	h ... Help (this)\n/
	f ... Fav (from Unyu)\n/
	z ... Zap (from Unyu)\n/
	r ... Reload (for debug)\n/
	'
}

//******************************************************************************
// key event
//******************************************************************************
OnKeyPress
{
	if ISFUNC('sys.key.' + reference[0]) {
		EVAL('sys.key.' + reference[0])
	}
}
sys.key.h
{
	sys.fnc.ShowHelp()
}
sys.key.f
{
	sys.save.favscript
	ERASEVAR('sys.save.favscript')
}
sys.key.z
{
	sys.save.zapscript
	ERASEVAR('sys.save.zapscript')
}
sys.key.r
{
	'\![reload,shiori]\0\s[0]\_qReloaded.\_q\e'
}
//******************************************************************************
// other
//******************************************************************************
OnSurfaceRestore
{
	Nostr.Hide()
}
//******************************************************************************
// Nostr
//******************************************************************************
ExternalEvent.OnNostr
{
	OnNostr()
}
OnNostr
{
	if sys.save.disable_sstp == 1 {
		'\C\e'
		return
	}
	Nostr.Clear()
	_protocol_version = reference[0]
	_avatar_url = ''
	case _protocol_version {
		when 'Nostr/0.3' {
			_kind = reference[1]
			case _kind {
				when 'note' {
					sys.save.kind = 1
				}
				when 'reaction' {
					sys.save.kind = 7
				}
				others {
					'\e'
					return
				}
			}
			sys.save.content = reference[2]
			sys.save.name = reference[3]
			sys.save.display_name = reference[4]
			_avatar_url = reference[5]
		}
		when 'Nostr/0.4' {
			_kind = reference[1]
			_supported_kinds = (1, 7)
			if ASEARCH(_kind, _supported_kinds) == -1 {
				'\e'
				return
			}
			sys.save.kind = _kind
			sys.save.content = reference[2]
			sys.save.name = reference[3]
			sys.save.display_name = reference[4]
			_avatar_url = reference[5]
			sys.save.note = reference[6]
			sys.save.npub = reference[7]
		}
		others {
			'\e'
			return
		}
	}
	if sys.save.content == ''{
		Nostr.Hide()
		return
	}
	_filename = ''
	_filepath = ''
	if _avatar_url == '' {
		_filepath = Nostr.DefaultAvatarPath()
	}
	else {
		_a = SPLITPATH(SPLIT(_avatar_url, '?')[0])
		_filename = sys.save.npub + '_' + _a[2] + _a[3]
		_filepath = GETSETTING('coreinfo.path') + '\var\' + _filename
	}
	if FSIZE(_filepath) >= 0 {
		Nostr.Resize(_filepath)
		Nostr.ShowTalk()
	}
	else {
		"\![execute,http-get,%(_avatar_url),--async=save_avatar,--file=%(_filename)]\e"
	}
}
OnExecuteHTTPFailure
{
	_filepath = Nostr.DefaultAvatarPath()
	if FSIZE(_filepath) >= 0 {
		Nostr.Resize(_filepath)
		Nostr.ShowTalk()
	}
}
OnExecuteHTTPComplete
{
	if reference[0] == 'get' && reference[1] == 'save_avatar' {
		Nostr.Resize(reference[3])
		Nostr.ShowTalk()
	}
	elseif reference[0] == 'get' && reference[1] == 'zap' {
		_invoice = reference[3]
		_path_b = GETSETTING('coreinfo.path') + 'var\invoice_b.png'
		_path_a = GETSETTING('coreinfo.path') + 'var\invoice_a.png'
		_ = SAORI('saori/qrcode/saori_qrcode.dll', 'image', _invoice, _path_b)
		_ = SAORI('saori/saori-resized-png/resizedpngmini.dll', 'ToResizedPng', _path_b, _path_a, '300', '300')
		"\0\b[2]\_q\_b[%(_path_a),centerx,centery]\e"
		sys.save.disable_sstp = 1
	}
}
Nostr.Resize
{
	_filepath = _argv[0]
	_saori_path = 'saori\saori-resized-png\resizedpngmini.dll'
	_type = FUNCTIONEX(_saori_path, 'GetImageType', _filepath)
	if _type == 'UNKNOWN' {
		_filepath = Nostr.DefaultAvatarPath()
	}
	_r = FUNCTIONEX(_saori_path, 'GetImageInfo', _filepath)
	_width = valueex[0]
	_height = valueex[1]
	if _r != '0' {
		Nostr.Clear()
		return
	}
	_width_after = -1
	_height_after = -1
	if _width < _height {
		_height_after = 128
	}
	elseif _width > _height {
		_width_after = 128
	}
	else {
		_width_after = 128
		_height_after = 128
	}
	_path_after = GETSETTING('coreinfo.path') + '..\..\shell\master\surface1.png'
	_r = FUNCTIONEX(_saori_path, 'ToResizedPng', _filepath, _path_after, _width_after, _height_after)
	if _r != '0' {
		Nostr.Clear()
		return
	}
}
Nostr.ShowTalk
{
	case sys.save.kind {
		_note = AYATEMPLATE.EscapeText(sys.save.note)
		_npub = AYATEMPLATE.EscapeText(sys.save.npub)
		_kind = AYATEMPLATE.EscapeText(sys.save.kind)
		_content = AYATEMPLATE.EscapeText(sys.save.content)
		when 1 {
			if sys.save.content != '' {
				"\![reload,shell]\0\s[1]\![bind,透明度,半透明,0]\_q\f[bold,true]%(SHIORI3FW.EscapeAllTags(sys.save.display_name))\f[bold,default] /
					@%(SHIORI3FW.EscapeAllTags(sys.save.name))\n\n\_q/
					\![set,balloonwait,0]%(SHIORI3FW.EscapeTagsWithoutNewLine(sys.save.content))/
					\_q\_l[0,0]\f[align,right]/
					\_a[fav,%(_note),%(_npub),%(_kind),%(_content)]\_b[%(GETSETTING('coreinfo.path') + 'image\star.png'),inline,--clipping=0 0 16 16]\_a/
					\_a[zap,%(_note),%(_npub),%(_kind),%(_content)]\_b[%(GETSETTING('coreinfo.path') + 'image\lightning.png'),inline,--clipping=0 0 16 16]\_a/
					\_a[close]\_b[%(GETSETTING('coreinfo.path') + 'image\cross.png'),inline,--clipping=0 0 16 16]\_a/
					\e"
				sys.save.favscript = "\![raise,OnAnchorSelectEx,,fav,%(_note),%(_npub),%(_kind),%(_content)]\e"
				sys.save.zapscript = "\![raise,OnAnchorSelectEx,,zap,%(_note),%(_npub),%(_kind),%(_content)]\e"
			}
		}
		when 7 {
			_height = ''
			_color = ''
			if sys.save.content == ''
				sys.save.content = '+'
			if sys.save.content == '+'
				sys.save.content = '♥'
			if sys.save.content == '♥'
				_color = '\f[color,red]'
			if STRLEN(sys.save.content) <= 2
				_height = '\f[height,500%]'
			"\![reload,shell]\0\s[1]\![bind,透明度,半透明,0]\_q\f[bold,true]%(SHIORI3FW.EscapeAllTags(sys.save.display_name))\f[bold,default] /
				@%(SHIORI3FW.EscapeAllTags(sys.save.name))\n\n/
				%(_height)%(_color)\f[align,center]%(SHIORI3FW.EscapeAllTags(sys.save.content))/
				\_l[0,0]\f[align,right]/
				\_a[fav,%(_note),%(_npub),%(_kind),%(_content)]\_b[%(GETSETTING('coreinfo.path') + 'image\star.png'),inline,--clipping=0 0 16 16]\_a/
				\_a[close]\_b[%(GETSETTING('coreinfo.path') + 'image\cross.png'),inline,--clipping=0 0 16 16]\_a/
				\e"
		}
		others {
			Nostr.Hide()
		}
	}
	Nostr.Clear()
}
OnAnchorSelectEx
{
	if ISFUNC('sys.asex.' + reference[1]) {
		EVAL('sys.asex.' + reference[1])
	}
}
sys.asex.fav
{
	_note = ''
	_nevent = ''
	if RE_MATCH(reference[2], '^note1.+$')
		_note = reference[2]
	elseif RE_MATCH(reference[2], '^nevent1.+$')
		_nevent = reference[2]
	_npub = reference[3]
	_kind = reference[4]
	_content_note = reference[5]
	_content_fav = '⭐'
	_url = 'https://nostr-unyu.vercel.app/api/query_fav'
	if _note != ''
		_url += "?note=%(_note)"
	elseif _nevent != ''
		_url += "?nevent=%(_nevent)"
	_url += "&npub=%(_npub)"
	_url += "&kind=%(_kind)"
	_url += "&content=%(_content_fav)"
	"\![execute,http-post,%(_url),--async=fav]/
			\![set,trayballoon/
			,--text=%(AYATEMPLATE.EscapeText(_content_note))/
			,--title=ReactionComplete/
			,--icon=info]\e"
}
sys.asex.zap
{
	_note = ''
	_nevent = ''
	if RE_MATCH(reference[2], '^note1.+$')
		_note = reference[2]
	elseif RE_MATCH(reference[2], '^nevent1.+$')
		_nevent = reference[2]
	_npub = reference[3]
	_kind = reference[4]
	_content_note = reference[5]
	_sats = 10
	_url = 'https://nostr-unyu.vercel.app/api/query_zap'
	if _note != ''
		_url += "?note=%(_note)"
	elseif _nevent != ''
		_url += "?nevent=%(_nevent)"
	_url += "&npub=%(_npub)"
	_url += "&sats=%(_sats)"
	"\![execute,http-get,%(_url),--async=zap,--nofile=UTF-8]/
			\![set,trayballoon/
			,--text=%(AYATEMPLATE.EscapeText(_content_note))/
			,--title=ZapRequest/
			,--icon=info]\e"
	sys.save.disable_sstp = 1
}
sys.asex.close
{
	Nostr.Hide()
}
OnBalloonClose
{
	Nostr.Hide()
}
OnBalloonTimeout
{
	Nostr.Hide()
}
Nostr.Hide
{
	sys.save.disable_sstp = ''
	'\0\s[0]\![bind,透明度,半透明,1]\e'
}
Nostr.DefaultAvatarPath
{
	GETSETTING('coreinfo.path') + '..\..\shell\master\default.png'
}
Nostr.Clear
{
	sys.save.kind = ''
	sys.save.content = ''
	sys.save.name = ''
	sys.save.display_name = ''
	sys.save.note = ''
	sys.save.npub = ''
	sys.save.disable_sstp = ''
}
OnSystemUnload.Nostr
{
	ERASEVAR(/
		'sys.save.kind',/
		'sys.save.content',/
		'sys.save.name',/
		'sys.save.display_name',/
		'sys.save.note',/
		'sys.save.npub',/
		'sys.save.npub',/
		'sys.save.disable_sstp'/
		'sys.save.zapscript'/
	)
	foreach FENUM('.\var\'); _path {
		FDEL('.\var\' + _path)
	}
}
SHIORI3FW.EscapeTagsWithoutNewLine{
	_ESCAPE_TAG_3 = CHR(0x5,0x5)
	_txt=_argv[0]
	_txt=REPLACE(_txt,'\\',ESCAPE_TAG_1)
	_txt=REPLACE(_txt,'\%',ESCAPE_TAG_2)
	_txt=REPLACE(_txt,'\n',_ESCAPE_TAG_3)
	_txt=REPLACE(_txt,'\','\\')
	_txt=REPLACE(_txt,'%','\%')
	_txt=REPLACE(_txt,_ESCAPE_TAG_3,'\n')
	_txt=REPLACE(_txt,ESCAPE_TAG_2,'\%')
	_txt=REPLACE(_txt,ESCAPE_TAG_1,'\\')
	_txt
}
