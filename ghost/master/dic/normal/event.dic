//******************************************************************************
// required event
//******************************************************************************
OnFirstBoot          { '\1\s[10]\0\s[0]\e' }
OnShellChanged       { '\1\s[10]\0\s[0]\e' }
OnWindowStateRestore { '\1\s[10]\0\s[0]\e' }
OnGhostChanged       { OnBoot() }
OnBoot               { '\1\s[10]\0\s[0]\e' }
OnClose              { '\1\s[10]\0\s[0]\_w[500]\-' }

//******************************************************************************
// update event
//******************************************************************************
OnUpdateBegin        { '\0\s[0]\_qUpdate Begin.\e' }
OnUpdateComplete     { '\0\s[0]\_q' + SHIORI3FW.EscapeAllTags(reference[0]) + '.\e' }
OnUpdateFailure      { '\0\s[0]\_q' + SHIORI3FW.EscapeAllTags(reference[0]) + '.\e' }
OnUpdateReady
{
	_n = 0
	if sender == 'embryo' {
		_n = TOINT(reference[0]) + 1
	} else {
		_n = TOINT(reference[0])
	}
	if _n > 1 {
		'\0\s[0]\_q' + _n + ' files exist.\_q\e'
	} elseif _n == 1 {
		'\0\s[0]\_q' + _n + ' file exists.\_q\e'
	}
}

//******************************************************************************
// mouse event
//******************************************************************************
OnMouseDoubleClick
{
	AYATEMPLATE.MouseEventExec('MouseDoubleClick');
}
MouseDoubleClick0
{
	'\0\s[0]\![bind,透明度,半透明,0]\_q/
	\![*]\__q[OpenAngolmois]Open Angolmois\__q\n/
	\![*]\__q[OpenNostryu]Open Nostrゅう\__q\n\n/
	\![*]\__q[Close]Close\__q\n/
	'
}
//******************************************************************************
// select event
//******************************************************************************
OnChoiceSelect
{
	_id = reference[0]
	if ISFUNC('sys.cs.' + _id) {
		EVAL('sys.cs.' + _id)
	}
}
sys.cs.OpenAngolmois
{
	'\![open,browser,https://nikolat.github.io/angolmois/]\e'
}
sys.cs.OpenNostryu
{
	'\![raiseplugin,a80c8840-f39e-11ee-96b0-0800200c9a66,OnMenuExec]\e'
}
sys.cs.Close
{
	'\0\![bind,透明度,半透明,1]\e'
}

//******************************************************************************
// key event
//******************************************************************************
OnKeyPress
{
	if ISFUNC('sys.key.' + reference[0]) {
		EVAL('sys.key.' + reference[0])
	}
}
sys.key.r
{
	'\![reload,shiori]\0\s[0]\_qReloaded.\_q\e'
}
//******************************************************************************
// other
//******************************************************************************
OnSurfaceRestore
{
	Nostr.Hide()
}
//******************************************************************************
// Nostr
//******************************************************************************
ExternalEvent.OnNostr
{
	OnNostr()
}
OnNostr
{
	_protocol_version = reference[0]
	_avatar_url = ''
	case _protocol_version {
		when 'Nostr/0.1' {
			sys.save.note = reference[1]
			sys.save.name = reference[2]
			sys.save.display_name = reference[3]
			_avatar_url = reference[4]
		}
		when 'Nostr/0.3' {
			_kind = reference[1]
			if _kind != 'note' {
				Nostr.Clear()
				'\e'
				return
			}
			sys.save.note = reference[2]
			sys.save.name = reference[3]
			sys.save.display_name = reference[4]
			_avatar_url = reference[5]
		}
	}
	if sys.save.note == ''{
		Nostr.Clear()
		'\e'
		return
	}
	_filename = ''
	_filepath = ''
	if _avatar_url == '' {
		_filepath = Nostr.DefaultAvatarPath()
	}
	else {
		_a = SPLITPATH(SPLIT(_avatar_url, '?')[0])
		_filename = _a[2] + _a[3]
		_filepath = GETSETTING('coreinfo.path') + '\var\' + _filename
	}
	if FSIZE(_filepath) >= 0 {
		Nostr.Resize(_filepath)
		Nostr.ShowTalk()
	}
	else {
		"\![execute,http-get,%(_avatar_url),--async=save_avatar,--file=%(_filename)]\e"
	}
}
OnExecuteHTTPFailure
{
}
OnExecuteHTTPComplete
{
	if reference[0] != 'get' || reference[1] != 'save_avatar'
		return
	Nostr.Resize(reference[3])
	Nostr.ShowTalk()
}
Nostr.Resize
{
	_filepath = _argv[0]
	_saori_path = 'saori\saori-resized-png\resizedpngmini.dll'
	_type = FUNCTIONEX(_saori_path, 'GetImageType', _filepath)
	if _type == 'UNKNOWN' {
		_filepath = Nostr.DefaultAvatarPath()
	}
	_r = FUNCTIONEX(_saori_path, 'GetImageInfo', _filepath)
	_width = valueex[0]
	_height = valueex[1]
	if _r != '0' {
		Nostr.Clear()
		return
	}
	_width_after = -1
	_height_after = -1
	if (_width > _height) {
		_height_after = 128
	}
	elseif (_width < _height) {
		_width_after = 128
	}
	else {
		_width_after = 128
		_height_after = 128
	}
	_path_after = GETSETTING('coreinfo.path') + '..\..\shell\master\surface1.png'
	_r = FUNCTIONEX(_saori_path, 'ToResizedPng', _filepath, _path_after, _width_after, _height_after)
	if _r != '0' {
		Nostr.Clear()
		return
	}
}
Nostr.ShowTalk
{
	if (sys.save.note != '')
		"\![reload,shell]\0\s[1]\![bind,透明度,半透明,0]\_q\f[bold,true]%(SHIORI3FW.EscapeAllTags(sys.save.display_name))\f[bold,default] /
		@%(SHIORI3FW.EscapeAllTags(sys.save.name))\n\n\_q/
		\![set,balloonwait,0]%(SHIORI3FW.EscapeDangerousTags(sys.save.note))/
		\e"
	Nostr.Clear()
}
OnBalloonClose
{
	Nostr.Hide()
}
OnBalloonTimeout
{
	Nostr.Hide()
}
Nostr.Hide
{
	'\0\s[0]\![bind,透明度,半透明,1]\e'
}
Nostr.DefaultAvatarPath
{
	GETSETTING('coreinfo.path') + '..\..\shell\master\default.png'
}
Nostr.Clear
{
	sys.save.note = ''
	sys.save.name = ''
	sys.save.display_name = ''
}
OnSystemUnload.Nostr
{
	ERASEVAR(/
		'sys.save.note',/
		'sys.save.name',/
		'sys.save.display_name'/
	)
	foreach FENUM('.\var\'); _path {
		FDEL('.\var\' + _path)
	}
}
